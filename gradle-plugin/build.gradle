plugins {
  id 'groovy'
  id 'maven-publish'
  id 'java-gradle-plugin'
  id 'com.gradle.plugin-publish'
  id 'com.palantir.git-version'
}

java {
  toolchain {
    languageVersion.set(JavaLanguageVersion.of(8))
  }
}

group = 'com.needhamsoftware.unojar'
version = '1.1.0'

configurations.all {
  resolutionStrategy.cacheChangingModulesFor 0, 'seconds'
}

repositories {
  mavenLocal()
  mavenCentral()
}

dependencies {
  implementation 'com.needhamsoftware.unojar:core:' + project.version
  implementation 'commons-io:commons-io:2.11.0'
  implementation 'org.apache.commons:commons-lang3:3.12.0'

  implementation gradleApi()
}

gradlePlugin {
  plugins {
    'unojar' {
      id = 'com.needhamsoftware.unojar'
      displayName = 'Uno-Jar'
      description = 'Single jar packaging based on a JarClassLoader. Unlike maven shade and gradle shadow, this form of packaging does not intermix classes into a single directory, and thereby maintains a degree of separation between libraries with distinct licensing concerns. Does not require write access to the filesystem like capsule'
      implementationClass = 'com.needhamsoftware.unojar.gradle.UnoJarPlugin'
    }
  }
}
jar.manifest.attributes getManifestAttributes()

@SuppressWarnings("GroovyAssignabilityCheck")
def getManifestAttributes() {
  def gitDetails = versionDetails();
  return [
      'Implementation-Version': version,
      'Build-Tool': "Gradle ${gradle.gradleVersion}",
      'Build-Revision': gitDetails.gitHashFull + (gitDetails.clean ? '' : ' (with uncommitted files)'),
      'Created-By': "Gradle ${gradle.gradleVersion}",
      'Build-Jdk': "${System.properties['java.version']} (${System.properties['java.vendor']} ${System.properties['java.vm.version']})",
      'Build-OS': "${System.properties['os.name']} ${System.properties['os.arch']} ${System.properties['os.version']}"]
}

publishing {
  publications {
    mavenJava(MavenPublication) {
      groupId 'com.needhamsoftware.unojar'
      artifactId 'gradle-plugin'
      version project.version
      from components.java
    }
  }
}

pluginBundle {
  website = 'https://github.com/nsoft/uno-jar'
  vcsUrl = 'https://github.com/nsoft/uno-jar.git'
  tags = ['jar', 'executable-jar', 'onejar', 'one-jar', 'shade', 'shadow', 'fatjar', 'uberjar','capsule']
}
