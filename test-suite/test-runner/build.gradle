import org.gradle.api.tasks.testing.logging.TestLogEvent

plugins {
    id("java")
}

dependencies {
    testImplementation("junit:junit:4.13.2")
    implementation("org.apache.commons:commons-lang3:3.12.0")
}


if (!project.hasProperty('ant.home') ) {
    throw new GradleException("\n" +
        "\n**********************************************************************************************************" +
        "\nPlease set ant.home property to the bin directory of your ant distribution in your local gradle properties" +
        "\nThis can be accomplished via a gradle.properties in /uno-jar/test-suite or via ~/.gradle/gradle.properties" +
        "\nas per your personal preference" +
        "\n**********************************************************************************************************\n")
}

def antCmd = project.getProperty('ant.home')
if (System.properties['os.name'].toLowerCase().contains('windows')) {
    antCmd = project.getProperty("ant.home") + "ant.bat"
} else {
    antCmd = project.getProperty("ant.home") + "ant"
}

tasks {
    tasks.register("antBuildTestMain", Exec.class) {
        workingDir '../test-main'
        commandLine antCmd, 'clean', 'packageUnoJar'
    }

    tasks.register("antBuildTestMainDirWithSlash", Exec.class) {
        workingDir '../test-main-dir-with-slash'
        commandLine antCmd, 'clean', 'packageUnoJar'
    }

    tasks.register("antBuildTestLog4jPlugin", Exec.class) {
        workingDir '../test-log4j-plugin'
        commandLine antCmd, 'clean', 'packageUnoJar'
    }

    tasks.register("antBuildTestLog4jMrJar", Exec.class) {
        workingDir '../test-log4j-mr-jar'
        commandLine antCmd, 'clean', 'packageUnoJar'
    }
}

tasks.test {
    dependsOn(
        ":groovy-dsl:build",
        ":kotlin-dsl:build",
        ":test-main:build",
        ":test-main-dir-with-slash:build",
        ":test-log4j-plugin:build",
        ":test-log4j-mr-jar:build",
        "antBuildTestMain",
        "antBuildTestMainDirWithSlash",
        "antBuildTestLog4jPlugin",
        "antBuildTestLog4jMrJar"
    )
    testLogging {
        events = [TestLogEvent.PASSED, TestLogEvent.FAILED, TestLogEvent.SKIPPED]
    }
}
